/* Tori Mondello
 Robota Psyche 19 March 
 Evolution Project 
 
 */

FlowField f;
ArrayList<Ant> ants = new ArrayList<Ant>();

Food food;

PImage sand;

void setup() {
  size (1200, 800);
  f = new FlowField(15);

  food = new Food(width/2, height/2);  //create food at center of screen

  sand = loadImage("sand.jpg"); //load background image

  // Make 100 ants to start with
  for (int i = 0; i < 100; i++) {
    ants.add(new Ant(random(width), random(height), 
      random(.04, .08), //size adjustment
      random(0, 0.1), //steering force
      1000, //amount of life
      100)); //death threshold 
  }
}


void draw() {
  image(sand, width/2, height/2); //draw background image
  food.display();
  
  for (Ant v : ants) {
    v.applyBehaviors(ants);
    v.seek(food.getLocation());
    v.flock(ants);
    v.follow(f); // Apply the steering force to follow the flow field
    v.update(); // Update the velocity and location, based on the acceleration generated by the steering force
    v.feed(food.getLocation()); 
    v.display(); // display the vehicle
  }

  for (int i = ants.size() - 1; i >= 0; i--) {
    Ant v = ants.get(i);
    v.consumeEnergy(); //make the ants energy level go down
    if (!v.alive) {   //if energy level gets too low, consider them dead and remove them
      println("DEATH"); 
      ants.remove(v);
    }
  }
  if ((frameCount % 100) == 0) {  //every so often, make a new ant
    Ant v = ants.get(int (random(ants.size()))); 
    DNA dna = v.getDNA();    //get the DNA from a random ant
    dna.mutate();  //allow for a chance of mutation


    if (dna.mutateChance <=5) {
      ants.add(new Ant(  //create new ant from regular DNA
        random(width), 
        random(height), 
        dna.bSize, 
        dna.maxF, 
        dna.initialEnergy, 
        dna.deadAt     
        ));
      println("NEW LIFE");
    }
    if (dna.mutateChance >=6) { //create new mutated ant 

      float sizeM = random(-0.04, .04);
      float forceM = random(-0.02, .02);
      float energyM = random(-400, 400);
      float deathM = random(-50, 300);
      //println("mutated size" + " " + sizeM); //for debugging to test the randomization values

      
      ants.add(new Ant(
        random(width), //location
        random(height), 
        (dna.bSize + sizeM), //size
        (dna.maxF + forceM), //force
        (dna.initialEnergy + energyM), 
        (dna.deadAt + deathM)     
        ));
      println("NEW MUTATED LIFE");
    }
  }
}
